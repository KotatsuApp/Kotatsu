plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-kapt'
	id 'kotlin-parcelize'
}

android {
	compileSdkVersion 32
	buildToolsVersion '32.0.0'
	namespace 'org.koitharu.kotatsu'

	defaultConfig {
		applicationId 'org.koitharu.kotatsu'
		minSdkVersion 21
		targetSdkVersion 32
		versionCode 405
		versionName '3.2'
		generatedDensities = []
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		kapt {
			arguments {
				arg 'room.schemaLocation', "$projectDir/schemas".toString()
			}
		}
	}
	buildTypes {
		debug {
			applicationIdSuffix = '.debug'
		}
		release {
			multiDexEnabled false
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	buildFeatures {
		viewBinding true
	}
	sourceSets {
		androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
		freeCompilerArgs += [
				'-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
				'-opt-in=kotlinx.coroutines.FlowPreview',
				'-opt-in=kotlin.contracts.ExperimentalContracts',
		]
	}
	lint {
		abortOnError false
		disable 'MissingTranslation', 'PrivateResource'
	}
	testOptions {
		unitTests.includeAndroidResources = true
		unitTests.returnDefaultValues = false
	}
}
dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
	implementation('com.github.nv95:kotatsu-parsers:a83f3a5178') {
		exclude group: 'org.json', module: 'json'
	}

	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

	implementation 'androidx.core:core-ktx:1.7.0'
	implementation 'androidx.activity:activity-ktx:1.4.0'
	implementation 'androidx.fragment:fragment-ktx:1.4.1'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
	implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
	implementation 'androidx.lifecycle:lifecycle-service:2.4.1'
	implementation 'androidx.lifecycle:lifecycle-process:2.4.1'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
	implementation 'androidx.preference:preference-ktx:1.2.0'
	implementation 'androidx.work:work-runtime-ktx:2.7.1'
	implementation 'com.google.android.material:material:1.6.0-rc01'
	//noinspection LifecycleAnnotationProcessorWithJava8
	kapt 'androidx.lifecycle:lifecycle-compiler:2.4.1'

	implementation 'androidx.room:room-runtime:2.4.2'
	implementation 'androidx.room:room-ktx:2.4.2'
	kapt 'androidx.room:room-compiler:2.4.2'

	implementation 'com.squareup.okhttp3:okhttp:4.9.3'
	implementation 'com.squareup.okio:okio:3.0.0'

	implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl:4.3.2'
	implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:4.3.2'

	implementation 'io.insert-koin:koin-android:3.1.6'
	implementation 'io.coil-kt:coil-base:1.4.0'
	implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
	implementation 'com.github.solkin:disk-lru-cache:1.4'

	debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'
	testImplementation 'io.insert-koin:koin-test-junit4:3.1.5'

	androidTestImplementation 'androidx.test:runner:1.4.0'
	androidTestImplementation 'androidx.test:rules:1.4.0'
	androidTestImplementation 'androidx.test:core-ktx:1.4.0'
	androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'
	androidTestImplementation 'androidx.room:room-testing:2.4.2'
}
