plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-kapt'
	id 'kotlin-parcelize'
}

android {
	compileSdkVersion 32
	buildToolsVersion '32.0.0'
	namespace 'org.koitharu.kotatsu'

	defaultConfig {
		applicationId 'org.koitharu.kotatsu'
		minSdkVersion 21
		targetSdkVersion 32
		versionCode 410
		versionName '3.3.1'
		generatedDensities = []
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		kapt {
			arguments {
				arg 'room.schemaLocation', "$projectDir/schemas".toString()
			}
		}
	}
	buildTypes {
		debug {
			applicationIdSuffix = '.debug'
		}
		release {
			multiDexEnabled false
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	buildFeatures {
		viewBinding true
	}
	sourceSets {
		androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_1_8.toString()
		freeCompilerArgs += [
			'-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
			'-opt-in=kotlinx.coroutines.FlowPreview',
			'-opt-in=kotlin.contracts.ExperimentalContracts',
			'-opt-in=coil.annotation.ExperimentalCoilApi',
		]
	}
	lint {
		abortOnError false
		disable 'MissingTranslation', 'PrivateResource', 'NotifyDataSetChanged'
	}
	testOptions {
		unitTests.includeAndroidResources = true
		unitTests.returnDefaultValues = false
	}
}
afterEvaluate {
	compileDebugKotlin {
		kotlinOptions {
			freeCompilerArgs += ['-opt-in=org.koitharu.kotatsu.parsers.InternalParsersApi']
		}
	}
}
dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
	implementation('com.github.nv95:kotatsu-parsers:8a3b6df91d') {
		exclude group: 'org.json', module: 'json'
	}

	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2'

	implementation 'androidx.core:core-ktx:1.8.0'
	implementation 'androidx.activity:activity-ktx:1.5.0-rc01'
	implementation 'androidx.fragment:fragment-ktx:1.5.0-rc01'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0-rc01'
	implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.0-rc01'
	implementation 'androidx.lifecycle:lifecycle-service:2.5.0-rc01'
	implementation 'androidx.lifecycle:lifecycle-process:2.5.0-rc01'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.recyclerview:recyclerview:1.2.1'
	implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
	implementation 'androidx.preference:preference-ktx:1.2.0'
	implementation 'androidx.work:work-runtime-ktx:2.7.1'
	implementation 'com.google.android.material:material:1.7.0-alpha02'
	//noinspection LifecycleAnnotationProcessorWithJava8
	kapt 'androidx.lifecycle:lifecycle-compiler:2.5.0-rc01'

	implementation 'androidx.room:room-runtime:2.4.2'
	implementation 'androidx.room:room-ktx:2.4.2'
	kapt 'androidx.room:room-compiler:2.4.2'

	implementation 'com.squareup.okhttp3:okhttp:4.10.0'
	implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.9.3'
	implementation 'com.squareup.okio:okio:3.1.0'

	implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl:4.3.2'
	implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:4.3.2'

	implementation 'io.insert-koin:koin-android:3.2.0'
	implementation 'io.coil-kt:coil-base:2.1.0'
	implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
	implementation 'com.github.solkin:disk-lru-cache:1.4'

	implementation 'ch.acra:acra-mail:5.9.3'
	implementation 'ch.acra:acra-dialog:5.9.3'

	debugImplementation 'org.jsoup:jsoup:1.15.1'
	debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2'

	androidTestImplementation 'androidx.test:runner:1.4.0'
	androidTestImplementation 'androidx.test:rules:1.4.0'
	androidTestImplementation 'androidx.test:core-ktx:1.4.0'
	androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.3'

	androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.2'
	androidTestImplementation 'io.insert-koin:koin-test:3.2.0'
	androidTestImplementation 'io.insert-koin:koin-test-junit4:3.2.0'

	androidTestImplementation 'androidx.room:room-testing:2.4.2'
	androidTestImplementation 'com.squareup.moshi:moshi-kotlin:1.13.0'
}