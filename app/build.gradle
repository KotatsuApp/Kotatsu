plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'kotlin-kapt'
	id 'kotlin-parcelize'
	id 'dagger.hilt.android.plugin'
}

android {
	compileSdk = 33
	buildToolsVersion = '33.0.2'
	namespace = 'org.koitharu.kotatsu'

	defaultConfig {
		applicationId 'org.koitharu.kotatsu'
		minSdkVersion 21
		targetSdkVersion 33
		versionCode 559
		versionName '5.3.2'
		generatedDensities = []
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

		kapt {
			arguments {
				arg 'room.schemaLocation', "$projectDir/schemas".toString()
			}
		}
	}
	buildTypes {
		debug {
			applicationIdSuffix = '.debug'
		}
		release {
			multiDexEnabled true
			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	buildFeatures {
		viewBinding true
	}
	sourceSets {
		androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
		main.java.srcDirs += 'src/main/kotlin/'
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
		coreLibraryDesugaringEnabled true
	}
	kotlinOptions {
		jvmTarget = JavaVersion.VERSION_17.toString()
		freeCompilerArgs += [
			'-opt-in=kotlin.ExperimentalStdlibApi',
			'-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
			'-opt-in=kotlinx.coroutines.FlowPreview',
			'-opt-in=kotlin.contracts.ExperimentalContracts',
			'-opt-in=coil.annotation.ExperimentalCoilApi',
		]
	}
	lint {
		abortOnError true
		disable 'MissingTranslation', 'PrivateResource', 'NotifyDataSetChanged', 'SetJavaScriptEnabled'
	}
	testOptions {
		unitTests.includeAndroidResources true
		unitTests.returnDefaultValues false
		kotlinOptions {
			freeCompilerArgs += ['-opt-in=org.koitharu.kotatsu.parsers.InternalParsersApi']
		}
	}
}
afterEvaluate {
	compileDebugKotlin {
		kotlinOptions {
			freeCompilerArgs += ['-opt-in=org.koitharu.kotatsu.parsers.InternalParsersApi']
		}
	}
}
dependencies {
	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

	//noinspection GradleDependency
	implementation('com.github.KotatsuApp:kotatsu-parsers:92bfc7e9fa') {
		exclude group: 'org.json', module: 'json'
	}

	implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'

	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'androidx.core:core-ktx:1.10.1'
	implementation 'androidx.activity:activity-ktx:1.7.2'
	implementation 'androidx.fragment:fragment-ktx:1.6.0'
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
	implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
	implementation 'androidx.lifecycle:lifecycle-service:2.6.1'
	implementation 'androidx.lifecycle:lifecycle-process:2.6.1'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
	implementation 'androidx.recyclerview:recyclerview:1.3.0'
	implementation 'androidx.viewpager2:viewpager2:1.1.0-beta02'
	implementation 'androidx.preference:preference-ktx:1.2.0'
	implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'
	implementation 'com.google.android.material:material:1.9.0'
	//noinspection LifecycleAnnotationProcessorWithJava8
	kapt 'androidx.lifecycle:lifecycle-compiler:2.6.1'

	// TODO https://issuetracker.google.com/issues/254846063
	implementation 'androidx.work:work-runtime-ktx:2.8.1'
	//noinspection GradleDependency
	implementation('com.google.guava:guava:32.0.0-android') {
		exclude group: 'com.google.guava', module: 'failureaccess'
		exclude group: 'org.checkerframework', module: 'checker-qual'
		exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
	}

	implementation 'androidx.room:room-runtime:2.5.2'
	implementation 'androidx.room:room-ktx:2.5.2'
	//noinspection KaptUsageInsteadOfKsp
	kapt 'androidx.room:room-compiler:2.5.2'

	implementation 'com.squareup.okhttp3:okhttp:4.11.0'
	implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.11.0'
	implementation 'com.squareup.okio:okio:3.3.0'

	implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl:4.3.2'
	implementation 'com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:4.3.2'

	implementation 'com.google.dagger:hilt-android:2.46.1'
	kapt 'com.google.dagger:hilt-compiler:2.46.1'
	implementation 'androidx.hilt:hilt-work:1.0.0'
	kapt 'androidx.hilt:hilt-compiler:1.0.0'

	implementation 'io.coil-kt:coil-base:2.4.0'
	implementation 'io.coil-kt:coil-svg:2.4.0'
	implementation 'com.github.KotatsuApp:subsampling-scale-image-view:1b19231b2f'
	implementation 'com.github.solkin:disk-lru-cache:1.4'
	implementation 'io.noties.markwon:core:4.6.2'

	implementation 'ch.acra:acra-http:5.10.1'
	implementation 'ch.acra:acra-dialog:5.10.1'

	debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.11'

	testImplementation 'junit:junit:4.13.2'
	testImplementation 'org.json:json:20230618'
	testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'

	androidTestImplementation 'androidx.test:runner:1.5.2'
	androidTestImplementation 'androidx.test:rules:1.5.0'
	androidTestImplementation 'androidx.test:core-ktx:1.5.0'
	androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'

	androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'

	androidTestImplementation 'androidx.room:room-testing:2.5.2'
	androidTestImplementation 'com.squareup.moshi:moshi-kotlin:1.15.0'

	androidTestImplementation 'com.google.dagger:hilt-android-testing:2.46.1'
	kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.46.1'
}
